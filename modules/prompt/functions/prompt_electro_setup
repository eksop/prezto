#
# A simple theme that displays relevant, contextual information.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Rajat Shukla <shk.rajat@gmail.com>
#

# Load dependencies.
pmodload 'helper'

function _xt_displaytime() {
  local T=$1
  local D=$((T/60/60/24))
  local H=$((T/60/60%24))
  local M=$((T/60%60))
  local S=$((T%60))
  (( $D > 0 )) && printf '%dd ' $D
  (( $H > 0 )) && printf '%dh ' $H
  (( $M > 0 )) && printf '%dm ' $M
  printf '%ds' $S
}

function _xt_set_pwd() {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_xt_pwd="$MATCH"
    unset MATCH
  else
    _prompt_xt_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function _xt_set_user_host() {
  if [[ "$SSH_CONNECTION" != '' ]]; then
    _prompt_xt_user_host="%(!:%{%F{red}%}:%{%F{green}%})%n%{%F{black}%}%B Ξ%b %{%F{blue}%}%m %{%F{black}%}%BΞ%b "
  elif [[ $UID -eq 0 ]]; then
    _prompt_xt_user_host="%(!:%{%F{red}%}:%{%F{green}%})%n%{%F{black}%}%B Ξ%b %{%F{blue}%}%m %{%F{black}%}%BΞ%b "
  else
    _prompt_xt_user_host=""
  fi
}

function _xt_set_timer() {
  if [ $xt_timer ]; then
    timer_show=$(_xt_displaytime ($(($SECONDS - $xt_timer))))
    _xt_timer_show="%F{green}${timer_show}%f %F{black}%BΞ%b%f "
    unset xt_timer
  fi
}

function prompt_electro_preexec {
  xt_timer=${xt_timer:-$SECONDS}
}

function prompt_electro_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Set timers
  _xt_set_timer

  # Format PWD.
  _xt_set_pwd

  # Set user and host
  _xt_set_user_host

  # VCS info
  vcs_info
}

function prompt_electro_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_electro_precmd
  add-zsh-hook preexec prompt_electro_preexec

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{7}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%F{1}❯%F{3}❯%F{2}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{3}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{2}❮%F{3}❮%F{1}❮%f%b'

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' stagedstr '%F{yellow} ✔%f'
  zstyle ':vcs_info:*' unstagedstr '%F{red} ✘%f'
  zstyle ':vcs_info:*' actionformats '%F{black}%BΞ%%b%f %F{blue}%b%f%c%u %F{yellow}(%a)%f '
  zstyle ':vcs_info:*' formats "%F{black}%BΞ%%b%f %F{blue}%b%f%c%u "
  zstyle ':vcs_info:*' nvcsformats "%b%f"

  # Define prompts.
  #

  # ✘
  PROMPT='%{%F{cyan}%}%B┌─[%b ${_prompt_xt_user_host}%{%F{green}%}${_prompt_xt_pwd} ${vcs_info_msg_0_}%{%F{cyan}%}%B]%b
%{%F{cyan}%}%B└─[%b %(?:%{%F{green}%}✓:%{%F{red}%}×) %{%F{cyan}%}%B]%b %{%(#~%{%F{red}%}~%{%F{yellow}%})%}%B%(!:ϟ:λ) %{${reset_color}%}%b'

  PROMPT2='%{%F{cyan}%}%B◀ %{${reset_color}%}%b'

  RPROMPT='%{$(echotc UP 1)%}%{%F{cyan}%}%B[%b ${_xt_timer_show}%{%F{green}%}%T %{%F{cyan}%}%B]%b%{$(echotc DO 1)%}%{$reset_color%}%b'

  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? %{${reset_color}%}%b'
}

function prompt_electro_preview {
  local +h PROMPT=''
  local +h RPROMPT=''
  local +h SPROMPT=''

  editor-info 2>/dev/null
  prompt_preview_theme 'electro'
}

prompt_electro_setup "$@"
